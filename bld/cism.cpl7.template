#! /bin/csh -f
#-------------------------------------------------------------------------------
# Template to build scripts to create ASCII input files for CISM, and to
# compile CISM code into a library that can be used by CCSM.
#-------------------------------------------------------------------------------

if !(-d $CASEBUILD) mkdir $CASEBUILD

#-------------------------------------------------------------------------------
# setup
#-------------------------------------------------------------------------------

set NX = $LND_NX
set NY = $LND_NY

# ---------------------------------------------------------------------------
#  Figure out the gridfile
# ---------------------------------------------------------------------------

set gridfile  = UNSET
if ( $ATM_GRID =~ 48x96   )then
   set gridfile  = \$DIN_LOC_ROOT/glc/cism/topodata_48x96_USGS_070110.glc.nc
endif
if ( $ATM_GRID =~ 0.9x1.25)then
   set gridfile  = \$DIN_LOC_ROOT/glc/cism/topodata_0.9x1.25_USGS_070110.glc.nc
endif
if ( $ATM_GRID =~ 1.9x2.5 )then
   set gridfile  = \$DIN_LOC_ROOT/glc/cism/fracdata/fracdata_1.9x2.5_gx1v6_c090206.glc.nc
endif

if ( $gridfile  == "UNSET" )then
   echo "ERROR: unsupported grid = $ATM_GRID"
   exit -1
endif

# ---------------------------------------------------------------------------
#  Figure out the cismgridfile
# ---------------------------------------------------------------------------

if ($RUN_TYPE == startup) then
  if ( $GLC_GRID =~ gland20 || $GLC_GRID =~ gland10 || $GLC_GRID =~ gland5) then
     set cismgridfile = $GLC_GRID.input.nc
  endif
else if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
  set cismgridfile = ${RUN_REFCASE}.cism.r.${RUN_REFDATE}-00000.nc
endif

# ---------------------------------------------------------------------------
#  Figure out the paramfile
# ---------------------------------------------------------------------------

set paramfile = UNSET
if ( $GLC_GRID =~ gland20 || $GLC_GRID =~ gland10 || $GLC_GRID =~ gland5) then
   set paramfile = $CODEROOT/glc/cism/input_templates/ice.config.$GLC_GRID
endif

if ( $paramfile == "UNSET" ) then
   echo "ERROR: unsupported ice grid = $GLC_GRID"
   exit -1
endif

# ---------------------------------------------------------------------------
#  Create list of input files
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/cism.input_data_list << EOF
gridfile       = $gridfile
cismgrid_input = \$DIN_LOC_ROOT/glc/cism/$cismgridfile
EOF

# ---------------------------------------------------------------------------
#  Create namelist build script
# ---------------------------------------------------------------------------
cat >! $CASEBUILD/cism.buildnml.csh << EOF1
#! /bin/csh -f 
# ---------------------------------------------------------------------------
#  Create input text files to run CISM
# ---------------------------------------------------------------------------

set exedir = \$RUNDIR; cd \$exedir
set cismgrid_input = \$DIN_LOC_ROOT/glc/cism/$cismgridfile
set cism_history = \$CASE.cism.h

cp \$cismgrid_input \$exedir/$cismgridfile
chmod 644 \$exedir/$cismgridfile

# Input namelists
cat >! cism_in <<EOF
&files_nml
  paramfile   = 'cism.config'
/
&grid_nml
  horiz_grid_opt  = 'file'
  horiz_grid_file = '$gridfile'
  mask_varname    = 'LANDMASK'
  frac_varname    = 'LANDFRAC'
/
&time_manager_nml
  runid             = '\$CASE'
  dt_option         = 'steps_per_day'
  dt_count          = 1
  allow_leapyear    = .false.
  iyear0            = 1
  imonth0           = 1
  iday0             = 1
  ihour0            = 0
  iminute0          = 0
  isecond0          = 0
  date_separator    = '-'
  stop_option       = 'never'
/
EOF

# Input configuration file
cat >! cism.config <<EOF
EOF1
# Bring a copy of the ice.config file that the user can edit for their case
cat $paramfile >> $CASEBUILD/cism.buildnml.csh
cat            >> $CASEBUILD/cism.buildnml.csh << EOF1
EOF
if (\$CONTINUE_RUN == 'FALSE') then
cat >> cism.config <<EOF
[CF input]
name: $cismgridfile
EOF
endif
EOF1

# ---------------------------------------------------------------------------
#  Create script to build CISM model
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/cism.buildexe.csh << EOF2
#! /bin/csh -f 

set objdir        = \$OBJROOT/glc/obj
set sourcemod_dir = \$CASEROOT/SourceMods/src.cism

# ---------------------------------------------------------------------------
#  Create script to build CISM model
# ---------------------------------------------------------------------------

cd \$objdir

#------------------------------------------------------------------------------
# Build the library
#------------------------------------------------------------------------------
cat >! Filepath << EOF
\$CASEROOT/SourceMods/src.cism
\$CODEROOT/glc/cism/source_glc
\$CODEROOT/glc/cism/source_glimmer-cism
\$CODEROOT/glc/cism/source_slap
\$CODEROOT/glc/cism/mpi
EOF

gmake complib -j \$GMAKE_J MODEL=cism COMPLIB=\$LIBROOT/libglc.a -f \$CASETOOLS/Makefile MACFILE=\$CASEROOT/Macros.\$MACH || exit 2

EOF2
