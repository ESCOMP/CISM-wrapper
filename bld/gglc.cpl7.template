#! /bin/csh -f

if !(-d $CASEBUILD) mkdir $CASEBUILD

#-------------------------------------------------------------------------------
# setup
#-------------------------------------------------------------------------------

set NX = $GLC_NX
set NY = $GLC_NY

set climatefile = glint_example.LY.config
set paramfile   = ice.config
set glandfile   = gland20.input.nc

# ---------------------------------------------------------------------------
#  Figure out the gridfile
# ---------------------------------------------------------------------------

set gridfile = UNSET
if ( $ATM_GRID =~ 48x96)    set gridfile = \$DIN_LOC_ROOT/glc/gglc/topodata_48x96_USGS_070110.glc.nc
if ( $ATM_GRID =~ 0.9x1.25) set gridfile = /blhome/lipscomb/ccsm4_0_glc01/models/glc/gglc/input_templates/griddata/topodata_0.9x1.25_USGS_070110.glc.nc
if ( $ATM_GRID =~ 1.9x2.5 ) set gridfile = /blhome/lipscomb/ccsm4_0_glc01/models/glc/gglc/input_templates/griddata/topodata_1.9x2.5_USGS_061130.glc.nc

if ( $gridfile  == "UNSET" ) then
   echo "ERROR: unsupported grid = $ATM_GRID"
   exit -1
endif

# ---------------------------------------------------------------------------
#  Create resolved namelist 
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/gglc.input_data_list << EOF
climatefile = \$DIN_LOC_ROOT/glc/gglc/$climatefile
paramfile   = \$DIN_LOC_ROOT/glc/gglc/$paramfile
gridfile    = $gridfile
gland_input = \$DIN_LOC_ROOT/glc/gglc/$glandfile
EOF

cat >! $CASEBUILD/gglc.buildnml.csh << EOF1
#! /bin/csh -f 

set exedir = \$RUNDIR; cd \$exedir

cp \$DIN_LOC_ROOT/glc/gglc/$glandfile \$exedir/
chmod 644 \$exedir/$glandfile

cat >! gglc_in <<EOF
&files_nml
  climatefile = '\$DIN_LOC_ROOT/glc/gglc/$climatefile'
  paramfile   = '\$DIN_LOC_ROOT/glc/gglc/$paramfile'
/
&grid_nml
  horiz_grid_opt  = 'file'
  horiz_grid_file = '$gridfile'
  topo_varname    = 'TOPO'
/
&time_manager_nml
  runid             = '\$CASE'
  dt_option         = 'steps_per_day'
  dt_count          = 1
  allow_leapyear    = .false.
  iyear0            = 1
  imonth0           = 1
  iday0             = 1
  ihour0            = 0
  iminute0          = 0
  isecond0          = 0
  date_separator    = '-'
  stop_option       = 'never'
/
EOF

cat >! gglcx_in << EOF
$NX       !  i-direction global dimension
$NY       !  j-direction global dimension
2         !  decomp_type  1=1d-by-lat, 2=1d-by-lon, 3=2d, 4=2d evensquare, 11=segmented
0         !  num of pes for i (type 3 only)
0         !  length of segments (type 4 only)
EOF

EOF1

# ---------------------------------------------------------------------------
#  Create script to build dead model
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/gglc.buildexe.csh << EOF2
#! /bin/csh -f 

set objdir = \$OBJROOT/glc/obj; cd \$objdir

#------------------------------------------------------------------------------
# Build the library
#------------------------------------------------------------------------------
cat >! Filepath << EOF
\$CASEROOT/SourceMods/src.gglc
\$CODEROOT/glc/gglc/source_glc
\$CODEROOT/glc/gglc/source_glimmer
\$CODEROOT/glc/gglc/mpi
EOF

gmake complib -j \$GMAKE_J MODEL=gglc COMPLIB=\$LIBROOT/libglc.a -f \$CASETOOLS/Makefile MACFILE=\$CASEROOT/Macros.\$MACH || exit 2

EOF2
